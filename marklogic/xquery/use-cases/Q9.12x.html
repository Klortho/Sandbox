<html>
<head>
<link rel="stylesheet" href="use-cases.css">
</head>
<body>
<form target="resultFrame"
      method="POST"
      action="eval2.xqy">

<table width="100%">
<tr>
  <td class="title" colspan="2">XQuery Source</td>
</tr>
<tr>
  <td valign="top" colspan="2">
    <textarea name="queryInput" rows="16" cols="80">
(:  __________________________________________________________
 ::  (9.12)
 :: Write a function that generates a quarterly report from a collection
 :: that contains US purchase orders.
 :: __________________________________________________________
  :)
import schema namespace ipo="http://www.example.com/IPO" at "ipo.xsd";
declare namespace rpt="http://www.example.com/Report";

let $orders := doc('ipo.xml')/schema-element(ipo:purchaseOrder)
                  [@orderDate ge xs:date("1999-09-01")
                   and @orderDate le xs:date("1999-12-31")]
let $items := $orders/items/item
let $zips := distinct-values($orders/billTo/zip)
let $parts := distinct-values($items/@partNum)
return
 <rpt:purchaseReport>
  <rpt:regions>
    {
     for $zip in $zips
     order by $zip
     return
      <rpt:zip code="{$zip}">
       {
        for $part in $parts
        let $hits := $orders[ billTo/zip = $zip and items/item/@partNum = $part]
        let $quantity := sum($hits//item[@partNum=$part]/quantity)
        where count($hits) > 0
        order by $part
        return
         <rpt:part number="{$part}" quantity="{$quantity}"/>
       }
      </rpt:zip>
    }
  </rpt:regions>
  <rpt:parts>
   {
     for $part in $parts
     return
      <rpt:part number="{$part}">
       {
         string($items[@partNum = $part]/productName)
       }
      </rpt:part>   
   }
  </rpt:parts>
</rpt:purchaseReport>
            
    </textarea>
  </td>
</tr>
<tr>
  <td width=36%>
    <input type="submit" value="Submit"/>
  </td>
  <td class="note">
    Query generates XML
    (<a href="Q9.12.html">Switch to XHTML</a>)
  </td>
</tr>
</table>
</form>
</body>
</html>
