<html>
<head>
<link rel="stylesheet" href="use-cases.css">
</head>
<body>
<form target="resultFrame"
      method="POST"
      action="eval.xqy">

<table width="100%">
<tr>
  <td class="title" colspan="2">XQuery Source</td>
</tr>
<tr>
  <td valign="top" colspan="2">
    <textarea name="queryInput" rows="16" cols="80">
(:  __________________________________________________________
 ::  (9.12)
 :: Write a function that generates a quarterly report from a collection
 :: that contains US purchase orders. In the output document...
 :: __________________________________________________________
  :)
import schema namespace ipo="http://www.example.com/IPO" at "ipo.xsd";
declare namespace rpt="http://www.example.com/Report";
<table width="100%">
<tr>
  <td class="title" colspan="4">  Use case Q9.12: Quarterly report</td>
</tr>
{
let $orders := doc('ipo.xml')/schema-element(ipo:purchaseOrder)
                  [@orderDate ge xs:date("1999-09-01")
                   and @orderDate le xs:date("1999-12-31")]
let $items := $orders/items/item
let $zips := distinct-values($orders/billTo/zip)
let $parts := distinct-values($items/@partNum)
return
(
  <tr><td><table><tr><th>Regions</th></tr>
    {
     for $zip in $zips
     order by $zip
     return (
      <tr><th>Zip: {$zip}</th></tr>,
      <tr><td><table><tr><th>Number</th><th>Quantity</th></tr>
      { for $part in $parts
        let $hits := $orders[ billTo/zip = $zip and items/item/@partNum = $part]
        let $quantity := sum($hits//item[@partNum=$part]/quantity)
        where count($hits) > 0
        order by $part
        return <tr><td>{$part}</td><td>{$quantity}</td></tr>
      }
      </table></td></tr> )
    }
  </table></td></tr> 
  ,
  <tr><td><table>
    <tr><th>Parts</th></tr>
    <tr><td><table>
      <tr><th>Number</th><th>Name</th></tr>
   {
     for $part in $parts
     return <tr><td>{$part}</td><td>{string($items[@partNum = $part]/productName)}</td></tr>
   }
    </table></td></tr>
  </table></td></tr>
)
}
</table>
    </textarea>
  </td>
</tr>
<tr>
  <td width="36%">
    <input type="submit" value="Submit"/>
  </td>
  <td class="note">
    Query generates XHTML (<a href="Q9.12x.html">Switch to XML</a>)
  </td>
</tr>
</table>
</form>
</body>
</html>
