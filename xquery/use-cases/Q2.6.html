<html>
<head>
<link rel="stylesheet" href="use-cases.css">
</head>
<body>
<form target="resultFrame"
      method="POST"
      action="eval.xqy">

<table width="100%">
<tr>
  <td class="title" colspan="2">XQuery Source</td>
</tr>
<tr>
  <td valign="top" colspan="2">
    <textarea name="queryInput" rows="16" cols="80">
(:  __________________________________________________________
 ::  (Q2.6)
 ::  Make a nested list of the section elements in Book1,
 ::  preserving their original attributes and hierarchy.  Inside
 ::  each section element, include the title of the section and
 ::  an element that includes the number of figures
 ::  immediately contained in the section. 
 :: __________________________________________________________
  :)

declare function local:section-summary($book-or-section as element()*)
  as element()*
{
  for $section in $book-or-section
  return
    <section>
       { $section/@* }
       { $section/title }       
       <figcount>         
         { count($section/figure) }
       </figcount>                
       { local:section-summary($section/section) }                      
    </section>
};


declare function local:dump-section($s as element()) as element()
{
  <ul>
    <li>
    { 
      $s/title/text(), " (",
      if ($s/@id) then
        ( "id = ", xs:string($s/@id), ", ")
      else (),

      if ($s/@difficulty) then 
        (" difficulty = ", xs:string($s/@difficulty), ", ")
      else (),

      "  figure count = ", xs:decimal($s/figcount), " )",

      for $ss in $s/section return local:dump-section($ss)
    }
    </li>
  </ul>
};

<table width="100%">
<tr>
  <td class="title" colspan="2">  Use case Q2.6 result</td>
</tr>
<tr bgcolor="#eeeeff">
  <th>Section Summaries</th>
</tr>
<tr><td>
{
  for $s in doc("book.xml")/book
  for $ss in local:section-summary($s)
  return local:dump-section($ss)
}
</td></tr>
</table>

    </textarea>
  </td>
</tr>
<tr>
  <td width="36%">
    <input type="submit" value="Submit"/>
  </td>
  <td class="note">
    Query generates XHTML (<a href="Q2.6x.html">Switch to XML</a>)
  </td>
</tr>
</table>
</form>
</body>
</html>
